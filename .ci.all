#!/bin/csh -f
#
#  .ci.all checks in all the source files that are already under RCS control
#
# usage: .ci.all [-m message]
#
if ( $?PAGER ) then
	:
else
	setenv PAGER more
endif

if ( $#argv != 0 && "$1" != "-m" ) then
	cat <<!

usage: $0 [-m message ]
	checks-in all the source files ...
	If no -m option
	1) If some are checked out already - do a rcsdiff piped through "$PAGER"
	2) checks in
	Else
	1) checks in all with message
	   (good for wholesale fixes after using .fix)
	   The message should probably be a quoted string if there are spaces

!
	exit
endif

setenv thisdir `pwd`

foreach d ( `find . -type d -not \( -name "RCS" -o -name "*tmp" -prune \) \
	-print` )
	echo "====$d===="
	cd $d
	foreach f ( `rlog -L -R RCS/*,v` )
		set ff=`basename $f ,v`
		echo "----$ff----"
		if ( "$1" == "-m" ) then
			ci -u -m"$2" $ff
		else
			rcsdiff -u $ff | $PAGER
			echo "-S--$ff-------------------------------------------------"
			ci -u $ff
			echo "-E--$ff-------------------------------------------------"
		endif
	end
	cd $thisdir
end
exit
